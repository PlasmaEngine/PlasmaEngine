// MIT Licensed (see LICENSE.md).

enum SSRDebugType
{
  None,
  Reflections,
  HitPixels
}

class PostProcessSSR : LightningComponent
{
    [Property] var Active : Boolean = true;
    
    [Group("Settings")]
    [Property]
    // Max distance a ray can travel
    var RayDistance : Real = 128.0;
    
    [Group("Settings")]
    [Property]
    // Distance between the steps in the SSR
    var StepDistance : Real = 0.04;
    
    [Group("Settings")]
    [Property]
    // Optimization to prevent reflection above cutoff
    var RoughnessCutoff : Real = 0.85;
    
    [Group("Settings")]
    [Property]
    // Used to remove black pixels
    var DarknessCutoff : Real = 0.02;
    
    [Group("Settings")]
    [Property]
    // Used to control the blur from roughness
    var RoughnessBlur : Real = 10.0;
    
    [Group("Settings")]
    [Property]
    // Renders relfections at half resolution (Optimization)
    var HalfRes : Boolean = true;
    
    [Group("Debug")]
    [Property]
    var DebugType : SSRDebugType = SSRDebugType.None;
    
    [Property]
    var Exponent : Real = 2.0;
    
    function AddSSRRenderTasks(event : RenderTasksEvent, geometryBuffer0 : RenderTarget, geometryBuffer1 : RenderTarget, screenBuffer : RenderTarget, ssrBuffer : RenderTarget)
    {
        if(this.Active == false)
            return;
            
        var ssrRenderSize = event.ViewportSize;
        if(this.HalfRes)
            ssrRenderSize = ssrRenderSize/ 2;
            
        var ssrTemp : RenderTarget = event.GetRenderTarget(ssrRenderSize, TextureFormat.RGBA16f);
   
        event.AddRenderTaskClearTarget(ssrTemp, Real4.Zero);
                   
        var postSSR  = SSR();
        postSSR.GeometryBuffer0 = geometryBuffer0.Texture;
        postSSR.GeometryBuffer1 = geometryBuffer1.Texture;
        postSSR.ScreenBuffer = screenBuffer.Texture;
        postSSR.RoughnessCutoff = this.RoughnessCutoff;
        postSSR.Blur = this.RoughnessBlur;
        postSSR.EnvironmentBrdfLut = Texture.EnvironmentBrdfLut;
        postSSR.Itteractions = this.RayDistance;
        postSSR.StepDistance = this.StepDistance;
        
        event.AddRenderTaskPostProcess(ssrTemp, postSSR, "SSR");
        
        var blurPass = SSRBlur();
        blurPass.Texture = ssrTemp.Texture;
        blurPass.GBuffer0 = geometryBuffer0.Texture;
        blurPass.BlurAmount = this.RoughnessBlur;
        event.AddRenderTaskPostProcess(ssrBuffer, blurPass, "SSRBlur");               
        BlurTexture[SeparableBlurRadius25].Call(event, ssrBuffer);         
        BlurTexture[SeparableBlurRadius25].Call(event, ssrBuffer);     
        
        if(this.DebugType == SSRDebugType.Reflections)
        {
          var copyPass = CopyTarget();
          copyPass.Texture = ssrBuffer.Texture;
          event.AddRenderTaskPostProcess(screenBuffer, copyPass, "SSRDebug");
        }
        else if(this.DebugType == SSRDebugType.HitPixels)
        {
          var hitPixelDebug = SSRDebug();
          hitPixelDebug.SSRBuffer = ssrBuffer.Texture;
          event.AddRenderTaskPostProcess(screenBuffer, hitPixelDebug, "HitPixelDebug");
        }
        
        var ssrFinalCopy = SSRCopy();
        ssrFinalCopy.Texture = screenBuffer.Texture;
        event.AddRenderTaskPostProcess(screenBuffer, ssrFinalCopy, "ssrFinalCopy");
        
        ssrTemp.Release();
    }
    
    function Upsample(event : RenderTasksEvent, target0 : RenderTarget, target1 : RenderTarget)
    {
      var upsampleSettings = RenderSettings();
      upsampleSettings.ColorTarget = target1;
      
      upsampleSettings.BlendSettings.BlendMode = BlendMode.Separate;
      upsampleSettings.BlendSettings.BlendEquation = BlendEquation.Add;
      upsampleSettings.BlendSettings.SourceFactor = BlendFactor.One;
      upsampleSettings.BlendSettings.DestFactor = BlendFactor.One;
      upsampleSettings.BlendSettings.SourceFactorAlpha = BlendFactor.Plasma;
      upsampleSettings.BlendSettings.DestFactorAlpha = BlendFactor.One;
      
      var copyTarget = CopyTarget();    
      copyTarget.Texture = target0.Texture;
      event.AddRenderTaskPostProcess(upsampleSettings, copyTarget, "Upscale");
    }
  }
