// MIT Licensed (see LICENSE.md).

// Data required to run indirect lighting calculations.
struct ImageBasedLightingInputs
{
  var WorldNormal : Real3;
  var WorldViewDirection : Real3;
  
  var Albedo : Real3;
  var Metallic : Real;
  var Roughness : Real;
  var Specular : Real;
  var SpecularColor : Real3;
  
  var RenderDiffuse : Boolean;
  var RenderSpecular : Boolean;
  var SSR : Real4;
  var Intensity : Real;
}

// Indirect lighting using an image (skybox).
// The texture used for the skybox must use PreGenerated mip mapping for correct results,
// which stores pre-computed integrals in the mip levels for the purpose of lighting.
[Pixel][PostProcess]
struct ImageBasedLighting
{
  [AppBuiltInInput] var PerspectiveToView : Real4x4;
  [AppBuiltInInput] var ViewToWorld : Real4x4;
  [AppBuiltInInput] var ViewportSize : Real2;
  
  [StageInput] var Uv : Real2;
  [StageInput] var PerspectivePosition : Real4;
  
  [Input] var SSRTexture : SampledImage2d;
  [Input] var DitherTexture : SampledImage2d;
  [Input] var EnvironmentBrdfLut : SampledImage2d;
  [Input] var Skybox : SampledImageCube;
  [Input] var Intensity : Real = 1.0;
  [Input] var MaxIntensity : Real = 50;
  
  [Input] var GeometryBuffer0 : SampledImage2d;
  [Input] var GeometryBuffer1 : SampledImage2d;
  [Input] var GeometryBuffer2 : SampledImage2d;
  [Input] var GeometryBuffer3 : SampledImage2d;
  
  [Input] var RenderDiffuse : Boolean = true;
  [Input] var RenderSpecular : Boolean = true;
  
  [Output] var Target0 : Real4;
  
  function Main()
  {
    // Get values from geometry buffers
    var geometrySample0 = this.GeometryBuffer0.Sample(this.Uv);
    var geometrySample1 = this.GeometryBuffer1.Sample(this.Uv);
    var geometrySample2 = this.GeometryBuffer2.Sample(this.Uv);
    var geometrySample3 = this.GeometryBuffer3.Sample(this.Uv);
    
    var viewNormal = GeometryPass.GetNormal(geometrySample0);
    var viewPosition = Math.Multiply(this.PerspectiveToView, this.PerspectivePosition);
    var viewDirection = Math.Normalize(-viewPosition.XYZ / viewPosition.W);
    
    var iblInputs = ImageBasedLightingInputs();
    iblInputs.WorldNormal = Math.MultiplyNormal(this.ViewToWorld, viewNormal);
    iblInputs.WorldViewDirection = Math.MultiplyPoint(this.ViewToWorld, viewDirection);
    iblInputs.Albedo = GeometryPass.GetAlbedo(geometrySample1);
    iblInputs.Metallic = GeometryPass.GetMetallic(geometrySample1);
    iblInputs.Roughness = GeometryPass.GetRoughness(geometrySample0);
    iblInputs.Specular = GeometryPass.GetSpecular(geometrySample3);
    iblInputs.SpecularColor = GeometryPass.GetSpecularColor(geometrySample2);
    iblInputs.RenderDiffuse = this.RenderDiffuse;
    iblInputs.RenderSpecular = this.RenderSpecular;
    //iblInputs.SSR = this.SSRTexture.Sample(this.Uv);
    iblInputs.Intensity = this.Intensity;
    
    var color = ImageBasedLighting.Compute(iblInputs, &this.Skybox, &this.EnvironmentBrdfLut);
    color = Math.Min(color, Real3(this.MaxIntensity));
   
    var ssr = this.SSRTexture.Sample(this.Uv);
    color = Math.Lerp(color, ssr.XYZ, ssr.W);
    
    // Remove color banding
    color = Utility.ApplyDither(color, &this.DitherTexture, this.Uv, this.ViewportSize);
    
    this.Target0 = Real4(color, 1.0);
  }
  
  [Static]
  function Compute(inputs : ImageBasedLightingInputs, skybox : ref SampledImageCube, brdfLut : ref SampledImage2d) : Real3
  {
    // Directions for sampling cubemap
    var diffuseDirection = inputs.WorldNormal;
    var specularDirection = Math.ReflectAcrossVector(inputs.WorldViewDirection, inputs.WorldNormal);
    
    // Cubemap details
    var textureSize = skybox.SampleSize(0).X;
    var maxLod = Math.Log2(textureSize);
    
    var envDiffuse = Math.Lerp(skybox.SampleLod(diffuseDirection, maxLod).XYZ, inputs.SSR.XYZ, inputs.SSR.W);
    var diffuseColor = envDiffuse * inputs.Albedo;

    // Sample lod for specular is based on roughness unless screen space derivitives
    // are large enough then we sample with that mip level to prevent aliasing
    var dx = ShaderIntrinsics.Ddx(specularDirection * textureSize);
    var dy = ShaderIntrinsics.Ddy(specularDirection * textureSize);
    var mipLevel = Math.Sqrt(Math.Max(Math.LengthSq(dx), Math.LengthSq(dy)));
    mipLevel = 0.5 * Math.Log2(mipLevel) - 1.0;
    var sampleLod = Math.Max(inputs.Roughness * maxLod, mipLevel);
    var envSpecular = skybox.SampleLod(specularDirection, sampleLod).XYZ;
    
    // Specular calculation using split sum approximation
    var dotnv = Math.Saturate(Math.Dot(inputs.WorldNormal, inputs.WorldViewDirection));
    var brdfSpec = brdfLut.Sample(Real2(dotnv, inputs.Roughness)).XY;
    var metallicSpecular = envSpecular * (inputs.Albedo * brdfSpec.X + brdfSpec.YYY);
    var dialectricSpecular = envSpecular * (Real3(0.10) * brdfSpec.X + brdfSpec.YYY);
    
    metallicSpecular = Math.Lerp(metallicSpecular * inputs.Intensity, inputs.SSR.XYZ, inputs.SSR.W);
    dialectricSpecular = Math.Lerp(dialectricSpecular * inputs.Intensity, inputs.SSR.XYZ, inputs.SSR.W);
    
    // For debugging
    diffuseColor *= inputs.RenderDiffuse as Real;
    metallicSpecular *= inputs.RenderSpecular as Real;
    dialectricSpecular *= inputs.RenderSpecular as Real;
    
    // Linear combination between dialectric and metallic colors
    return Math.Lerp((diffuseColor * inputs.Intensity)  + dialectricSpecular * inputs.Specular, metallicSpecular, inputs.Metallic);
  }
}
